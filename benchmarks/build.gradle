apply plugin: 'kotlinx.benchmark'

apply from: rootProject.file("gradle/targets.gradle")

repositories {
    maven { url 'https://dl.bintray.com/kotlin/kotlinx' }
    maven { setUrl("https://dl.bintray.com/hotkeytlt/maven") }
    maven { url = uri("https://dl.bintray.com/47deg/helios") }
}

kotlin {
    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += '-Xno-param-assertions'
            }
        }
    }
    targets.jvm.compilations.all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                // parsers
                implementation project(":benchmarks:genericParser")
                implementation project(":benchmarks:charParser")
                implementation project(":benchmarks:byteParser")
                // Benchmark parts of shared-compiler
                implementation project(":compiler:shared-compiler")

                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
                implementation "org.jetbrains.kotlinx:kotlinx.benchmark.runtime:$kotlin_benchmark_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version"
                implementation "com.github.h0tk3y.betterParse:better-parse:$better_parse_version"
            }
        }
        jvmMain {
            dependencies {
                implementation "com.47deg:helios-core:$helios_version"
                implementation "com.47deg:helios-parser:$helios_version"
            }
        }
    }
}

benchmark {
    targets {
        register("jvm")
        // register("js")
    }
    configurations {
        collections {
            include("IntMap")
        }
        comparisons {
            include("JsonBench")
        }
        instructions {
            include("MapVsApply")
        }
    }
}