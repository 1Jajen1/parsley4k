plugins {
    id 'org.jetbrains.kotlinx.benchmark' version "$kotlin_benchmark_version"
}

apply from: rootProject.file("gradle/targets.gradle")

repositories {
    mavenCentral()
}

kotlin {
    targets.all {
        compilations.all {
            kotlinOptions {
                freeCompilerArgs += '-Xno-param-assertions'
            }
        }
    }
    targets.jvm.compilations.all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                // parsers
                implementation project(":benchmarks:genericParser")
                implementation project(":benchmarks:charParser")
                implementation project(":benchmarks:byteParser")
                // Benchmark parts of shared-compiler
                implementation project(":compiler:shared-compiler")

                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
                implementation "org.jetbrains.kotlinx:kotlinx-benchmark-runtime:$kotlin_benchmark_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_version"
                implementation "com.github.h0tk3y.betterParse:better-parse:$better_parse_version"
            }
        }
    }
}

benchmark {
    targets {
        register("jvm")
        // register("js")
    }
    configurations {
        collections {
            include("IntMap")
        }
        comparisons {
            include("JsonBench")
        }
        instructions {
            include("MapVsApply")
        }
    }
}